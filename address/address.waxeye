# Stable Waxeye seems to have a bug around `?(*X ...)`,
# so I replaced it with  `?(?X *X ...)`.
# Don't forget to follow nonterminals when you are checking against this.

Addr <-
  ?Seps *Seps ?FakePostcode ?(?(RepeatedPostcode +Seps) *(RepeatedPostcode +Seps) LastPostcode *Seps)
  TownStreet ?(+Sep Type) *Seps
  NumberEnding

NumberEnding <= ?Comment ?(Number ?Comment ?(?Sep *Sep Locator ?Comment)) *Seps

Comment <- ?Seps *Seps (:(+'/') *(!'/' .) :(+'/') (+Seps | !.) | :('(') (Conscription | *(![()] .)) :(')') *Seps)

FakePostcode <: '9999' +Seps

RepeatedPostcode <: Postcode &(+Seps Postcode)

LastPostcode <= Postcode

Postcode <= [1-8][0-9][0-9][0-9] | '9'[0-8][0-9][0-9] | '99'[0-8][0-9] | '999'[0-8]

TownStreet <= UTownStreet | CTownStreet

# Uppercase town and street
UTownStreet <= UTown +Seps UStreet
UTown <- Big +Big
UStreet <- BigNum *BigNum *( +Sepn NotTypeNumber +BigNum) ?'.'

# Camelcase or lowercase town and street
CTownStreet <= CTown *Seps CStreet | CTown +Seps NCStreet
CTown <- Big +Small
CStreet <- BigNum *BigSmallNum *( +Sepn NotTypeNumber +BigSmallNum) ?'.'
NCStreet <- +BigSmallNum *( +Sepn NotTypeNumber +BigSmallNum) ?'.'

NotTypeNumber <= !(?(Type *Seps) NumberEnding !.)

Number <=
  Km
  | Conscription
  | House ?(+Seps Conscription)
  | :('(') Conscription :(')') *Seps House

Km <- [1-9] *Num :(+Sepd "km" ?(?Sep *Sep "szelvény"))

House <=
  HouseNumber
  ?Comment
  (
    ReversedBuilding ?ReversedStairs ?ReversedOrNamedLevel ?ReversedDoor
  | ?ReversedBuilding ReversedStairs ?ReversedOrNamedLevel ?ReversedDoor
  | ?ReversedBuilding ?ReversedStairs ReversedLevel ?ReversedDoor
  | ?ReversedBuilding ?ReversedStairs ?ReversedOrNamedLevel ReversedDoor
  | ?(?Sepd *Sepd BuildingN BuildingSuf)
    ?(?Sepd *Sepd (StairsR | StairsL) StairsSuf)
    ?LevelDoor
  )
  :(?'.')

ReversedBuilding <= +Seps :("ép" ?"ület" ':') *Seps BuildingN
ReversedStairs <= +Seps StairsToken :(':') *Seps (StairsR | StairsL)
ReversedLevel <= +Seps LevelToken :(':') *Seps (Level | LevelN)
ReversedDoor <= +Seps :("a" ?("j" ?"tó") ':') *Seps DoorN
ReversedOrNamedLevel <= ReversedLevel | +Seps NamedLevel

LevelDoor <=
  ?Sepd *Sepd Level *Sepd :(?'/') *Sep DoorN ?DoorSuf
  | ?Sepd *Sepd LevelN *Sepd :('/') *Sep DoorN (DoorSuf | !. | !*Sep '/')
  | ?(?Sepd *Sepd Level) ?(?Sepd *Sepd DoorN DoorSuf)

HouseNumber <-
  [1-9] ?Num ?Num
  ?(?Sep *Sep '-' *Sep [1-9] ?Num ?Num)
  ?(+Seps Comment)
  ?SubNumberOrLetter
  :(?(?Sepd *Sepd "sz" ('.' | "ám")))

SubNumberOrLetter <=
  ?Sepd *Sepd :([/-]) *Sep SubLetter
  | ?Sepd *Sepd :('/') *Sep SubLetter
  | ?Sepd *Sepd SubLetter &(Sepd | !.) !(BuildingSuf | StairsSuf | LevelSuf)

SubNumber <- [1-9] | [1-9] Num
SubLetter <- [A-Za-z]

BuildingN <- Num ?Num | Small | Big | :('"') Small :('"') | :('"') Big :('"')
BuildingSuf <: ?Sepd *Sep ?(("sz." | "számú") ?' ') ?("iroda" ?' ') "é" ("p" ?"ület" ?'.' | Seps)
StairsR <- +[ivx] | +[IVX]
StairsL <- Big
StairsSuf <: ?'.' *Sep StairsToken ?'.'
StairsToken <: "lh" | "lph" | "lcs" | "lpház" | "lp" | "lép" | "lépcsőház"
Level <= NamedLevel ?LevelSuf | LevelN LevelSuf
NamedLevel <= Cellar | Basement | Ground | HighGround | HalfLevel | Roof | LevelR
LevelSuf <: ?'.' *Sep LevelToken ?'.'
LevelToken <: "e" ?("m" ?"elet")
Cellar <- :("pince" *Sepd ?("sz" ?("int" | ?'.')))
Basement <- :(("alagsor" | "as" | "alg" | "al") ?'.')
Ground <- :(("fs" ?("z" ?"t") | "földszint") ?'.')
HighGround <- :(("mf" ?("sz" ?"t") | "magasföldszint") ?'.')
HalfLevel <- :("f" ?("él" | '.' *Sep) "e" ?"melet" ?'.' | '1/2' *Sepd "em" ?'.')
Roof <- :("tetőtér" | "tetőszint" | "tt" ?'.') ?LevelSuf
LevelN <- Num ?Num
LevelR <- +[ivx] | +[IVX]

DoorN <- !Conscription Num ?Num ?Num
DoorSuf <: !(LocatorEnd !.) ?'.' *Sep ("a" ?("jt" ?"ó") | "sz" ?('.' | "ám") | '-' [eEaAöÖoO] "s " ?"kapu" "csengő")

Conscription <= ConscriptionHrsz | ConscriptionNum

ConscriptionNum <-
  ( Num Num Num +Num '/' +Num ?('/' Big ?('/' Num))
  | +Num '/' Num Num +Num
  | '0' *Num '/' +Num
  | +Num '/0' *Num
  ) :(?(?Sep *Sep '.') ?(?Sep *Sep "hrsz" ?'.'))

ConscriptionHrsz <-
  +Num ?('/' +Num *('/' +BigSmallNum))
  :(?(?Sep *Sep '.') *Sep "hrsz" ?'.')

Locator <- +(!LocatorType (BigSmallNum | '-')) LocatorEnd
LocatorEnd <= :(?(+[ \t.] "sz" ?"ámú")) *Sepd LocatorType

Sep <: [ \t]
Sepd <: Sep | '.'
Seps <: Sepn | ','
Sepn <= [ \t.-]
Small <= [a-záíűőüöúóé]
Big <= [A-ZÁÍŰŐÜÖÚÓÉ]
BigSmall <= Big | Small
BigSmallNum <= BigSmall | Num
BigNum <= Big | Num
Num <= [0-9]

Type <- "út" | "utca" | "u."
  | "útja"
  | "határút"
  | "tér" | "tere"
  | "tanya" | 't.'
  | "köz"
  | "főút"
  | "sor" | 's.'
  | "sétány"
  | "lakótelep" | "ltp."
  | "park" ?"ja"
  | "rakpart" | "rkp."
  | "körút" | "krt."
  | "sugárút" | "sgt."
  | "dűlő" | 'd.'
  | "fasor"
  | "körvasútsor"
  | "országút"

LocatorType <-
  "üzlet" | "üz." | "/ü."
  | "galéria"
  | "pavilon" | "pav."
  | "stand"
  | "mélygarázs"
  | "garázs"
  | "raktár"
  | "műhely"
  | "asztal"
  | "iroda"
