# Stable Waxeye seems to have a bug around `?(*X ...)`,
# so I replaced it with  `?(?X *X ...)`.
# Don't forget to follow nonterminals when you are checking against this.

Addr <-
  ?FakePostcode ?(?(RepeatedPostcode +Seps) *(RepeatedPostcode +Seps) LastPostcode *Seps)
  TownStreet ?(+Sep Type) *Seps
  NumberEnding

NumberEnding <= ?Comment ?(Number ?(?Sep *Sep Locator)) *Seps

Comment <- :(+'/') *(!'/' .) :(+'/') *Sepd

FakePostcode <: '9999' +Seps

RepeatedPostcode <: Postcode &(+Seps Postcode)

LastPostcode <= Postcode

Postcode <= [1-8][0-9][0-9][0-9] | '9'[0-8][0-9][0-9] | '99'[0-8][0-9] | '999'[0-8]

TownStreet <= UTownStreet | CTownStreet

# Uppercase town and street
UTownStreet <= UTown +Seps UStreet
UTown <- Big +Big
UStreet <- BigNum *BigNum *( +Sepn NotTypeNumber +BigNum) ?'.'

# Camelcase or lowercase town and street
CTownStreet <= CTown *Seps CStreet | CTown +Seps NCStreet
CTown <- Big +Small
CStreet <- BigNum *BigSmallNum *( +Sepn NotTypeNumber +BigSmallNum) ?'.'
NCStreet <- +BigSmallNum *( +Sepn NotTypeNumber +BigSmallNum) ?'.'

NotTypeNumber <= !(?(Type *Seps) NumberEnding !.)

Number <=
  Km
  | Conscription
  | House ?(+Seps Conscription)
  | :('(') Conscription :(')') *Seps House

Km <- [1-9] *Num :(+Sepd "km" ?(?Sep *Sep "szelvény"))

House <=
  HouseNumber
  ?(?Sepd *Sepd BuildingN)
  ?(?Sepd *Sepd StairsN)
  ?LevelDoor
  :(?'.')

LevelDoor <=
  ?Sepd *Sepd Level *Sepd :(?'/') *Sep DoorN ?DoorSuf
  | ?Sepd *Sepd LevelN *Sepd :('/') *Sep DoorN (DoorSuf | !. | !*Sep '/')
  | ?(?Sepd *Sepd Level) ?(?Sepd *Sepd DoorN DoorSuf)

HouseNumber <-
  [1-9] ?Num ?Num
  ?(?Sep *Sep '-' *Sep [1-9] ?Num ?Num)
  ?SubNumberOrLetter
  :(?(?Sepd *Sepd "sz" ('.' | "ám")))

SubNumberOrLetter <=
  ?Sepd *Sepd :('/') *Sep (SubNumber | SubLetter)
  | ?Sepd *Sepd SubLetter &(Sepd | !.) !(BuildingSuf | StairsSuf | LevelSuf)

SubNumber <- [1-9] | [1-9]Num
SubLetter <- [A-Za-z]

BuildingN <- (Num ?Num | Big) BuildingSuf
BuildingSuf <: ?Sepd *Sep "ép" ?"ület" ?'.'
StairsN <- (Roman | Big) StairsSuf
StairsSuf <: ?'.' *Sep ("lh" | "lph" | "lépcsőház") ?'.'
Level <= Ground | HighGround | LevelN LevelSuf | LevelR ?LevelSuf
LevelSuf <: ?'.' *Sep "e" ?("m" ?"elet") ?'.'
Ground <- :(("fsz" ?"t" | "földszint") ?'.')
HighGround <- :(("mfsz" ?"t" | "magasföldszint") ?'.')
LevelN <- Num ?Num
LevelR <- Roman

DoorN <- Num ?Num ?Num
DoorSuf <: !(LocatorEnd !.) ?'.' *Sep ("ajtó" | "sz" ?('.' | "ám"))

Conscription <= ConscriptionHrsz | ConscriptionNum

ConscriptionNum <-
  ( Num Num Num +Num '/' +Num
  | +Num '/' Num Num +Num
  | '0' *Num '/' +Num
  | +Num '/0' *Num
  ) :(?(?Sep *Sep '.') ?(?Sep *Sep "hrsz" ?'.'))

ConscriptionHrsz <-
  +Num ?('/' +Num *('/' +BigSmallNum))
  :(?(?Sep *Sep '.') *Sep "hrsz" ?'.')

Locator <- +(!LocatorType BigSmallNum) LocatorEnd
LocatorEnd <= :(?(+[ \t.] "sz" ?"ámú")) *Sepd LocatorType

Sep <: [ \t]
Sepd <: Sep | '.'
Seps <: Sepn | ','
Sepn <= [ \t.-]
Small <= [a-záíűőüöúóé]
Big <= [A-ZÁÍŰŐÜÖÚÓÉ]
BigSmall <= Big | Small
BigSmallNum <= BigSmall | Num
BigNum <= Big | Num
Num <= [0-9]
Roman <= +[IVX]

Type <- "út" | "utca" | "u."
  | "tér"
  | "köz"
  | "főút"
  | "sor" | "s."
  | "lakótelep" | "ltp."
  | "park" ?"ja"
  | "rakpart" | "rkp."
  | "tanya" | "t."
  | "körút" | "krt."
  | "fasor"

LocatorType <-
  "üzlet" | "üz." | "/ü."
  | "galéria"
  | "pavilon" | "pav."
  | "garázs"
  | "raktár"
