Addr <= ?FakePostcode ?(*(RepeatedPostcode +Seps) LastPostcode *Seps) TownStreet ?(+Sep Type) *Seps Number ?(*Sep Locator)

FakePostcode <: "9999" +Seps

RepeatedPostcode <: Postcode &(+Seps Postcode)

LastPostcode <= Postcode

Postcode <= [1-8][0-9][0-9][0-9] | '9'[0-8][0-9][0-9] | '99'[0-8][0-9] | '999'[0-8]

TownStreet <= UTownStreet | CTownStreet

# Uppercase town and street
UTownStreet <= UTown +Seps UStreet
UTown <- Big +Big
UStreet <- BigNum +BigNum *( +Sepn !(Type Sep) (+BigNum)) ?'.'

# Camel or lowercase town and street
CTownStreet <= CTown *Seps CStreet
CTown <- Big +Small
CStreet <- BigNum *BigSmallNum *( +Sepn !(Type Sep) (+BigSmallNum)) ?'.'

Type <- "út" | "utca" | "főút"

Number <= Km | Conscription | House ?(+Sep Conscription) | :("(") Conscription :(")") *Sep House

Km <- [1-9] *[0-9] :('.' *Sep "km")

House <= HouseNumber ?(+Sepd Level) ?(+Sepd Door) :(?'.')

HouseNumber <- [1-9] ?[0-9] ?[0-9] ?(*Sep '/' *Sep ([A-Za-z] | [1-9] | [1-9][0-9]))

Conscription <= ConscriptionHrsz | ConscriptionNum

ConscriptionNum <=
  ( [0-9][0-9][0-9] +[0-9] '/' +[0-9]
  | +[0-9] '/' [0-9][0-9] +[0-9]
  | '0' *[0-9] '/' +[0-9]
  | +[0-9] '/0' *[0-9]
  ) :(?(*Sep '.') ?(*Sep "hrsz" ?'.'))

ConscriptionHrsz <= +[0-9] '/' +[0-9] :(?(*Sep '.') *Sep "hrsz" ?'.')

Level <= Ground | LevelN :(? '.' *Sep "em" ?"elet" ?'.')
Ground <- ("fsz" ? "t" | "földszint") ?'.'
LevelN <- +[0-9]

Door <= DoorN :(?'.' *Sep "ajtó")
DoorN <- +[0-9]

Locator <- +BigSmallNum ?(+[ \t.] "számú") +[ \t.] ("üzlet" | "pavilon")

Sep <: [ \t]
Sepd <: Sep | '.'
Seps <: Sepn | ','
Sepn <= [ \t.-]
Small <= [a-záíűőüöúóé]
Big <= [A-ZÁÍŰŐÜÖÚÓÉ]
BigSmall <= Big | Small
BigSmallNum <= BigSmall | [0-9]
BigNum <= Big | [0-9]
